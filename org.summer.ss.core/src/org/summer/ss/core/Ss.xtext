/*******************************************************************************
 * Copyright (c) 2010 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.summer.ss.core.Ss with org.summer.dsl.xbase.Xbase

import "http://www.summer.org/dsl/model/xbase" as xbase
import "http://www.summer.org/dsl/model/xtype" as xtype
import "http://www.summer.org/dsl/model/types" as types
import "http://www.summer.org/dsl/model/xaml" as xaml

JvmModule returns types::JvmModule : 
	('package' packageName=QualifiedName)?
	'module' simpleName=ID '{'
	importSection=XImportSection? 
	root=XObjectElement?
	(contents+=(Type | DelegateType) 
	| (contents+= XStatment )  )*
	
	exportSection=XExportSection? 
	'}'
;

XImportSection returns xtype::XImportSection:
	(importDeclarations+=XImportDeclaration ';'?)+;

XImportDeclaration returns xtype::XImportDeclaration: 
	{xtype::XImportDeclaration}
	'import' 
		'{'
			((importItems += XImportItem (',' importItems += XImportItem)*)
			| wildcard ?= '*'
			)
		'}'
		'from' importedNamespace=QualifiedName
		'as' alias=ValidID
;

XImportItem returns xtype::XImportItem:
	{xtype::XImportItem}
	importedId = [types::JvmIdentifiableElement|ID] ('as' alias=ID)?
;

XExportSection returns xtype::XExportSection:
	(exportDeclarations+=XExportDeclaration ';'?)+;

XExportDeclaration returns xtype::XExportDeclaration: 
	'export' 
	(
		(
			('{'
				exportItems +=XExportItem (',' exportItems +=XExportItem)*
			'}'('from' importURI=STRING)?
			)
//			|
//			(exportItems +=XExportItem)
//			|
//			wildcard?='*'
		)
		|
		( 'from' importURI=STRING 'as' alias=ID)
	)
;

XExportItem returns xtype::XExportItem:
	exportedId =[types::JvmIdentifiableElement|ID] ('as' alias=ID)?
;

Type returns types::JvmDeclaredType :
	{types::JvmDeclaredType} annotations+=JvmAnnotation*   
	(
		{types::JvmGenericType.annotationInfo = current}
		(exported?='export')? modifiers+=ClassModofier*
	  	'class' simpleName=ValidID ('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?  
	   	("extends" extends=JvmParameterizedTypeReference)? 
	   	('implements' implements+=JvmParameterizedTypeReference (',' implements+=JvmParameterizedTypeReference)*)?'{'
	   		root=XObjectElement?
		   (members+=Member)*
	   	'}'
	 |
		{types::JvmInterfaceType.annotationInfo = current}
		(exported?='export')? //modifiers+=CommonModifier*
	  	'interface' simpleName=ValidID ('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?  
	   	('extends' implements+=JvmParameterizedTypeReference (',' implements+=JvmParameterizedTypeReference)*)?'{'
		   (members+=Member)*
	   	'}'
	 |
		{types::JvmRemoteType.annotationInfo = current}
		(exported?='export')? //modifiers+=CommonModifier*
	  	'remote' simpleName=ValidID ('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?  
	   	('extends' implements+=JvmParameterizedTypeReference (',' implements+=JvmParameterizedTypeReference)*)?'{'
		   (members+=Member)*
	   	'}'
	 |
		{types::JvmBeanType.annotationInfo = current}
		(exported?='export')? //modifiers+=CommonModifier*
	  	'bean' simpleName=ValidID ('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?  
	   	('extends' implements+=JvmParameterizedTypeReference (',' implements+=JvmParameterizedTypeReference)*)?'{'
		   (members+=Member)*
	   	'}'
	 |
 		{types::JvmStructType.annotationInfo = current}
		(exported?='export')? //modifiers+=CommonModifier*
	  	'struct' simpleName=ValidID ('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?  
	   	('implements' implements+=JvmParameterizedTypeReference (',' implements+=JvmParameterizedTypeReference)*)?
	   	'{'
		   (members+=Member)*
	   	'}'
	 |
		{types::JvmEnumerationType.annotationInfo = current}
		(exported?='export')? //modifiers+=CommonModifier*
	  	'enum' simpleName=ValidID '{'   
		   (members+=JvmEnumerationLiteral (',' members+=JvmEnumerationLiteral)*)? 
	   	'}'
	 |
	 	{types::JvmAnnotationType.annotationInfo = current}
	 	(exported?='export')? //modifiers+=CommonModifier*
		'annotation' simpleName=ValidID '{'
		   (members+=AnnotationField)*
	   	'}'
	)
;

DelegateType returns types::JvmDelegateType:
// 	{types::JvmDelegateType} annotations+=XAnnotation*  
	(exported ?= 'export')? 'delegate' 
	   	('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
	   	 returnType=JvmTypeReference
	   	 simpleName=ValidID
	    '('(parameters+=JvmFormalParameter (',' parameters+=JvmFormalParameter)*)? ')' 
	 ';'
;

AnnotationField returns types::JvmField :
	annotations+=JvmAnnotation* //modifiers+=CommonModifier* 
	( 
		type=JvmTypeReference simpleName=ValidID
	)
	('=' defaultValue=XLiteral)? ';'
;

Member returns types::JvmMember:
	{types::JvmMember} annotations+=JvmAnnotation*
	(
		(	
			{types::JvmField.annotationInfo = current} modifiers+=FieldModifier*
			(
				type=JvmTypeReference (simpleName=FeatureCallID |
				(indexer ?= 'this' '['(parameters+=JvmFormalParameter (',' parameters+=JvmFormalParameter)*)? ']'))
			)
			(
				(('=' defaultValue=XExpression)? ';')
				|
				(
				property ?= '{'
					'get' (get=XBlockStatment)? 
					('set' (set=XBlockStatment)?)?
				'}'
				)
			)
		) 
		|
	 	(
			{types::JvmOperation.annotationInfo = current} modifiers+=MethodModifier*
			'function' 
				('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
				returnType=JvmTypeReference
				 (simpleName=FeatureCallID | 
				 	(operator ?= 'operator' simpleName=Operators)
				 )
				'('(parameters+=JvmFormalParameter (',' parameters+=JvmFormalParameter)*)? ')'
					
					('throws' exceptions+=JvmTypeReference (',' exceptions+=JvmTypeReference)*)?
				(body=XBlockStatment 
					| ';'
				) 
		)
	 	| 
	 	(
			{types::JvmConstructor.annotationInfo = current} //modifiers+=CommonModifier*
			modifiers+=ConstructorModofier* 'constructor' 
			('<' typeParameters+=JvmTypeParameter (',' typeParameters+=JvmTypeParameter)* '>')?
			'(' (parameters+=JvmFormalParameter (',' parameters+=JvmFormalParameter)*)? ')'
			body=XBlockStatment
		)
		| (
			{types::JvmEvent.annotationInfo = current} modifiers+=FieldModifier*
			'event' type=JvmTypeReference simpleName=ValidID 
			(
				'{'
					'add' add=XBlockStatment 
					('remove' remove=XBlockStatment)?
				'}'
			)?
		)
	)
;

Operators:
	  OpMultiAssign
	| OpOr
	| OpAnd
	| OpEquality
	| OpCompare
	| ShiftOp
	| BinaryBitwiseOp
	| OpMulti
	| OpUnary
//	| PrefixOp
//	| IndexOp
//	| OpPostfix
;

JvmEnumerationLiteral returns types::JvmEnumerationLiteral: 
	simpleName=ValidID ('=' defaultValue = XNumberLiteral)?
;


ClassModofier:
	'abstract'|
	'native'
;

//CommonModifier:
////		'public' 
////		|
////		 'private' |
////		| 'protected' 
////		| 'package' 
////		| 'abstract' 
//		 'static' 
////		| 'export'
////		| 'dispatch' 
//		| 'final'
//		| 'native'
//		| 'override'
//		| 'virtaul'
//;

FieldModifier:
		'static' | 
		'final' |
		'virtual' |
		'override' |
		'transient' 
	    | 'public'
		| 'private' 
		| 'protected'
;

ConstructorModofier:
	'static'|
	'private'
	|'protected'
	|'public'
;

//EventModifier:
//		'static' | 
//		'const' |
//		'virtaul' |
//		'override' 
//;

MethodModifier:
		'override' | 'virtual' |'static' | 'native' |'overload' | 'abstract'
		    | 'public'
			| 'private' 
   			| 'protected'
; 

//MethodModifier:
//		'def' | 
//		'override' 
//; 

//CreateExtensionInfo:
//	'create' (name=ValidID ':')? createExpression=XExpression
//;

//MemberID:
//	ID 
//	| 'set' 
//	| 'get'
//	| 'add'
//	| 'remove'
//;

FeatureCallID:
	ID 
	| 'set' 
	| 'get'
	| 'add'
	| 'remove'
//	| 'ref'
//	| 'out'
//	| 'abstract' 
//	| 'annotation' 
//	| 'class' 
//	| 'create' 
//	| 'def' 
//	| 'dispatch' 
//	| 'enum'
// 	| 'extends' 
// 	| 'extension' 
//   	| 'final'
//	| 'implements' 
//    | 'import' 
//    | 'interface' 
//    | 'override' 
//    | 'package' 
//    | 'public'
//	| 'private' 
//   	| 'protected'
//	| 'static' 
//    | 'throws'
;

XElement returns xaml::XElement:
	XObjectElement
	|XAttributeElement
	;

XObjectElement returns xaml::XObjectElement:
	{xaml::XObjectElement}
	"<" type=[types::JvmType|QualifiedName]  
				properties+=XAbstractAttribute * =>  ((">" 
		(contents+=XElement	)*
	"</"  closeType=[types::JvmType|QualifiedName] ">") | "/>")
	;
XAttributeElement returns xaml::XElement:
	{xaml::XAttributeElement}
	"<" "(" type=[types::JvmType|QualifiedName ] ")""." field=[types::JvmField|ID]  => ((">"
		(contents+=XElement	)*
	"</" "(" closeType=[types::JvmType|QualifiedName ] ")""." closeField=[types::JvmField|ID] )  ">" |  "/>")
	;

XAbstractAttribute returns xaml::XAbstractAttribute:
	XGeneralAttribute
	|XAttachAttribute
	;

XAttachAttribute returns xaml::XAttachAttribute:
	"("type=[types::JvmType|QualifiedName] ")" '.' field=[types::JvmField|ID]  "=" value=XPropertyExpression
	;
XGeneralAttribute returns xaml::XGeneralAttribute:
	field=[types::JvmField|ID] "=" value=XPropertyExpression
	;

XMarkupExtenson returns xbase::XExpression:
	{xaml::XMarkupExtenson}
	"{"
		type=[types::JvmType|QualifiedName]
		properties+=XAbstractAttribute* 
	"}"
	;

XPropertyExpression returns xbase::XExpression:
	XStringLiteral 
	|XMarkupExtenson
	;	

